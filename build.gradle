buildscript {
    ext.kotlin_version = '1.1.4-3'
    ext {
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.16"
        classpath "gradle.plugin.com.github.youribonnaffe.gradle.format:gradle-format-plugin:1.5"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.5.2"
    }
}

ext.junitPlatformVersion = '1.0.0'
ext.junitJupiterVersion  = '5.0.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'kotlin'
apply plugin: 'scala'
apply plugin: "com.github.maiflai.scalatest"
apply plugin: 'com.github.youribonnaffe.gradle.format'
apply plugin: "com.diffplug.gradle.spotless"

springBoot {
    executable = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

idea.module.outputDir = compileJava.destinationDir
idea.module.inheritOutputDirs = true
processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources
afterEvaluate {
    tasks.getByName('spotlessCheck').dependsOn(tasks.getByName('spotlessApply'))
}
spotless {
    java {
        importOrder 'java', 'javax', 'org', 'com', 'com.diffplug', "org.flywaydb", 'org.springframework', 'io.vavr', 'com.atilika.kuromoji', 'me.ramswaroop', 'com.makowis' // A sequence of package names
        removeUnusedImports()
        eclipse().configFile 'spotless.eclipseformat.xml'   // XML file dumped out by the Eclipse formatter
    }
    kotlin {
        ktlint()
    }
    scala {
        scalafmt().configFile('scalafmt.conf')
    }
}

jar {
    baseName = 'shuzobot'
    version =  '0.2.0'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {

    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-jdbc');

    // spring security
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-security')

    // '/health' エンドポイントなどを自動で追加するライブラリです。
    // https://docs.spring.io/spring-boot/docs/current/reference/html/production-ready-endpoints.html
    compile('org.springframework.boot:spring-boot-starter-actuator')

    // use web socket
    compile("me.ramswaroop.jbot:jbot:3.0.2")

    // Spring Boot-Doma連携用
    compile('org.seasar.doma.boot:doma-spring-boot-starter:1.1.1')
    compile('mysql:mysql-connector-java:5.1.38')

    // flyway
    compile('org.flywaydb:flyway-core:4.2.0')

    // 形態素解析
    compile('com.atilika.kuromoji:kuromoji-ipadic:0.9.0')

    // Vavr
    compile('io.vavr:vavr:0.9.0')

    compile('org.springframework.boot:spring-boot-starter-tomcat')

    // UnitTest
    testCompile('org.springframework.boot:spring-boot-test')
    testCompile('com.github.sbrannen:spring-test-junit5:1.0.0')
    testCompile('org.mockito:mockito-core:2.+')
    testCompile('org.scala-lang:scala-library:2.12.3')
    testCompile('org.scalatest:scalatest_2.12:3.0.1')
    testRuntime 'org.pegdown:pegdown:1.4.2'
    testCompile("org.junit.platform:junit-platform-runner:${junitPlatformVersion}")

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

ext['thymeleaf.version'] = '3.0.7.RELEASE'
ext['thymeleaf-layout-dialect.version'] = '2.2.2'
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
